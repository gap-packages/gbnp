gap> ######################### BEGIN COPYRIGHT MESSAGE #########################
GBNP - computing Gröbner bases of noncommutative polynomials
Copyright 2001-2010 by Arjeh M. Cohen, Dié A.H. Gijsbers, Jan Willem
Knopper, Chris Krook. Address: Discrete Algebra and Geometry (DAM) group
at the Department of Mathematics and Computer Science of Eindhoven
University of Technology.

For acknowledgements see the manual. The manual can be found in several
formats in the doc subdirectory of the GBNP distribution. The
acknowledgements formatted as text can be found in the file chap0.txt.

GBNP is free software; you can redistribute it and/or modify it under
the terms of the Lesser GNU General Public License as published by the
Free Software Foundation (FSF); either version 2.1 of the License, or
(at your option) any later version. For details, see the file 'LGPL' in
the doc subdirectory of the GBNP distribution or see the FSF's own site:
https://www.gnu.org/licenses/lgpl.html
gap> ########################## END COPYRIGHT MESSAGE ##########################

gap> ### filename = "example25.g" 
gap> ### author amc
gap> ### amc last edited 1 June 2007

<#GAPDoc Label="Example25">
<Section Label="Example25"><Heading>Baur and Draisma's transformations</Heading>

<Listing><![CDATA[
gap> LoadPackage("gbnp", false);
true
gap> SetInfoLevel(InfoGBNP,0);
gap> SetInfoLevel(InfoGBNPTime,0);
]]></Listing>

The paper <Cite Key="MR2090062"/> by Baur and Draisma
uses the computation of a quotient
algebra of dimension 37, which we
repeat here.
The set of equations, after specialisation of the scalars to 1, 
is as follows.

<Listing><![CDATA[
gap> KI := [ [[[2,2]],[1]],
>         [[[1,1]],[1]],
>         [[[3,3]],[1]],
>         [[[1,2,1],[1]],[1,-1]],
>         [[[2,1,2],[2]],[1,-1]],
>         [[[3,2,3],[3]],[1,-1]],
>         [[[2,3,2],[2]],[1,-1]],
>         [[[1,3,1],[1]],[1,-1]],
>         [[[3,1,3],[3]],[1,-1]],
>         [[[1,2,3,1,2,3,1],[1,3,2,1,3,2,1],[1]],[1,1,-1]],
>         [[[3,1,2,3,1,2,3],[3,2,1,3,2,1,3],[3]],[1,1,-1]],
>         [[[2,3,1,2,3,1,2],[2,1,3,2,1,3,2],[2]],[1,1,-1]],
>       ];;
gap> PrintNPList(KI);
 b^2 
 a^2 
 c^2 
 aba - a 
 bab - b 
 cbc - c 
 bcb - b 
 aca - a 
 cac - c 
 abcabca + acbacba - a 
 cabcabc + cbacbac - c 
 bcabcab + bacbacb - b 
]]></Listing>

We carry out a traced Gröbner basis computation by use of
<Ref Func="SGrobnerTrace" Style="Text"/>,
and form the usual Gröbner basis by extracting the polynomials
from the traced polynomials using the field indicator <C>.pol</C>.
<Listing><![CDATA[
gap> GBT := SGrobnerTrace(KI);;
gap> GB := List([1..Length(GBT)], i -> GBT[i].pol);;
]]></Listing>

The dimension of the quotient algebra is computable with
<Ref Func="DimQA" Style="Text"/>.
<Listing><![CDATA[
gap> DimQA(GB,3);
37
]]></Listing>

In order to express the
last GB element, viz.
<Listing><![CDATA[
gap> PrintNP(GB[Length(GB)]);
 cabcabca + cbacba - ca 
]]></Listing>
as a combination of elements of <C>KI</C>,
we use <Ref Func="PrintTracePol" Style="Text"/>:
<Listing><![CDATA[
gap> PrintTracePol(GBT[Length(GBT)]);  
- G(9)bacba + cG(10) 
]]></Listing>


We compute matrices for left multiplication by generators 
using <Ref Func="MatricesQA" Style="Text"/>
and determine the minimal polynomial of the sum of the three matrices.

<Listing><![CDATA[
gap> B := BaseQA(GB,3,0);;
gap> M := MatricesQA(3,B,GB);;
gap> f := MinimalPolynomial(Rationals,M[1]+M[2]+M[3]);
x_1^7-6*x_1^5+9*x_1^3-3*x_1
gap> Factors(f);
[ x_1, x_1^6-6*x_1^4+9*x_1^2-3 ]
]]></Listing>

It turns out that there are three non-zero numbers <M>u,v,w</M>
such that the eigenvalues of the sum are <M>0,u,v,w,-u,-v,-w</M>.
This is the information used in  <Cite Key="MR2090062"/>.
</Section>
<#/GAPDoc>
